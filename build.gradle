apply plugin: 'java'
apply plugin: 'idea'

description = 'JMBE Audio Library build script'
sourceCompatibility = 1.8

subprojects {
    apply plugin: 'java'
    version = '1.0.0'
}

project(':api') {
    jar {
        baseName = rootProject.name + "-api"
        manifest {
            attributes('Implementation-Title': rootProject.name + ' API',
                       'Implementation-Version': project.version,
                       'Website': 'https://github.com/DSheirer/jmbe')
        }
        from rootProject.file('LICENSE')
    }
}

project(':jmbe') {
    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.25'
        compile 'org.slf4j:slf4j-simple:1.7.25'
        compile 'com.github.wendykierp:JTransforms:3.1'
        compile 'pl.edu.icm:JLargeArrays:1.6'
        compile project(':api')
    }

    jar {
        manifest {
            attributes('Implementation-Title': rootProject.name,
                       'Version': project.version,
                       'Site': 'https://github.com/DSheirer/jmbe')
        }
        from rootProject.file('LICENSE')
    }

    task copyLibrary(type: Copy){
        from jar
        into "$rootProject.buildDir/libs"
        println("Your finished JMBE library is here: $rootProject.buildDir/libs")
    }

    build.finalizedBy(copyLibrary)
}

//Removes the root project (empty) jar but retains the build/libs folders so that we can
//place sub-project jars in folder.
task deleteRootJar(type: Delete){
    delete jar
}

build.finalizedBy(deleteRootJar)

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

